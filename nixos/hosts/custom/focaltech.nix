{
  stdenv,
  fetchurl,
  rpm,
  cpio,
  glib,
  gusb,
  pixman,
  libgudev,
  nss,
  libfprint,
  lib,
  cairo,
  pkg-config,
  autoPatchelfHook,
  makePkgconfigItem,
  copyPkgconfigItems,
}:
let
  libso = "libfprint-2.so.2.0.0";
in
stdenv.mkDerivation rec {
  pname = "libfprint-focaltech";
  version = "1.94.9";

  src = fetchurl {
    url = "https://web.archive.org/web/20250314121447if_/https://raw.githubusercontent.com/ftfpteams/focaltech-linux-fingerprint-driver/refs/heads/main/Fedora_Redhat/libfprint-2-2_1.94.4%2Btod1_redhat_all_x64_20250219.install";
    sha256 = "0y7kb2mr7zd2irfgsmfgdpb0c7v33cb4hf3hfj7mndalma3xdhzn";
  };

  nativeBuildInputs = [
    autoPatchelfHook
    copyPkgconfigItems
    cpio
    pkg-config
    rpm
  ];

  buildInputs = [
    cairo
    glib
    gusb
    libfprint
    libgudev
    nss
    pixman
    stdenv.cc.cc
  ];

  unpackPhase = ''
    runHook preUnpack
    echo "Extracting embedded tar.gz using sed"

    sed '1,/^main \$@/d' $src > libfprint.tar.gz

    mkdir extracted
    tar -xzf libfprint.tar.gz -C .
  '';

  pkgconfigItems = [
    (makePkgconfigItem rec {
      name = "libfprint-2";
      inherit version;
      cflags = [ "-I${variables.includedir}/libfprint-2" ];
      libs = [
        "-L${variables.libdir}"
        "-lfprint-2"
      ];
      variables = rec {
        prefix = "${placeholder "out"}";
        includedir = "${prefix}/include";
        libdir = "${prefix}/lib";
      };
    })
  ];

  passthru = {
    driverPath = "/lib";
  };

  installPhase = ''
    runHook preInstall

    install -Dm444 usr/lib64/${libso} -t $out/lib

    ln -s -T $out/lib/${libso} $out/lib/libfprint-2.so
    ln -s -T $out/lib/${libso} $out/lib/libfprint-2.so.2

    cp -r ${libfprint}/lib/girepository-1.0 $out/lib
    cp -r ${libfprint}/include $out

    runHook postInstall
  '';

  meta = with lib; {
    description = "FocalTech libfprint driver (Fedora variant)";
    homepage = "https://github.com/ftfpteams/focaltech-linux-fingerprint-driver";
    platforms = platforms.linux;
    license = licenses.unfree;
  };
}
